buildscript {
   ext.kotlin_version = '1.3.60'
   repositories {
      mavenCentral()
   }
}

plugins {
   id 'org.jetbrains.kotlin.multiplatform'
   id "com.github.node-gradle.node" version "2.2.0"
}

repositories {
   mavenCentral()
   mavenLocal()
   maven {
      url "https://oss.sonatype.org/content/repositories/snapshots"
   }
}

kotlin {

   js()

   sourceSets {

      jsMain {
         dependencies {
            implementation(kotlin("stdlib-js"))
         }
      }

      jsTest {
         dependencies {
            implementation(project(":kotest-core"))
         }
      }
   }
}

compileKotlinJs.configure {
   kotlinOptions {
      moduleKind = 'commonjs'
   }
}

task copyJsDependencies(type: Copy, dependsOn: compileTestKotlinJs) {
   from compileKotlinJs.destinationDir
   into "${buildDir}/node_modules"

   def configuration = configurations.jsTestRuntimeClasspath
   from(files {
      configuration.collect { File file ->
         file.name.endsWith(".jar")
            ? zipTree(file.absolutePath).matching {
            include '*.js'
            include '*.js.map'
         } : files()
      }
   }.builtBy(configuration))
}

node {
   download = true
}

task installMocha(type: NpmTask) {
   args = ['install', 'mocha']
}

task runMocha(type: NodeTask, dependsOn: [installMocha, compileTestKotlinJs, copyJsDependencies]) {
   script = file('node_modules/mocha/bin/mocha')
   args = ["kotest-framework-adapter.js", compileTestKotlinJs.outputFile]
}

jsTest.dependsOn runMocha
